# 1. THE HUB PROVIDER (Used only for authentication)
# Terraform Cloud automatically authenticates this one using the TFC_GCP_... env vars.
provider "google" {
  alias   = "tfc_hub"
  project = "tfc-identity" # The Hub Project ID
}

# This isn't a provider block, but a Data Source. 
# It uses the "Hub" provider (above) to request a token for the "Spoke" service account. 
# It effectively says: "Hey Hub, please get me a key for Spoke 1."
data "google_service_account_access_token" "spoke_token" {
  provider               = google.tfc_hub
  target_service_account = "tfc-spoke-1@tltestlab-project1.iam.gserviceaccount.com"
  scopes                 = ["cloud-platform"]
  lifetime               = "3600s"
}

# The Resource Manager (The "Spoke").
# This is the default provider that the rest of my Terraform code will use. 
# It takes the token generated by the Bridge and uses it to manage the actual resources.
provider "google" {
  access_token = data.google_service_account_access_token.spoke_token.access_token
  project      = "tltestlab-project1" # The Spoke Project ID
}